import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'clickstack'
apply plugin: 'release'
apply plugin: 'maven'
// apply plugin: 'maven-publish' wit for http://issues.gradle.org/browse/GRADLE-2919

group = 'com.cloudbees.clickstack'

// clickstackId MUST be declared in gradle.properties because the following expression does not create a project property
// clickstackId = "wildfly${wildflyMajorVersion}"
// see doc at http://stackoverflow.com/questions/7709695/trying-to-understand-gradle-project-properties

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

mainClassName = 'com.cloudbees.clickstack.wildfly.Setup'

configurations {
    deployerJars

    clickStackPackage.ext.clickStackFolder = ""

    clickStackRuntimeJavaAgent.ext.clickStackFolder = "deps/javaagent-lib"

    clickStackRuntime.ext.clickStackFolder = "deps/wildfly-lib"
    clickStackRuntimePostgresql.ext.clickStackFolder = "deps/wildfly-lib-postgresql"
    clickStackRuntimeMySql.ext.clickStackFolder = "deps/wildfly-lib-mysql"
    clickStackRuntimeMemcache.ext.clickStackFolder = "deps/wildfly-lib-memcache"

    clickStackControl.ext.clickStackFolder = "deps/control-lib"
}

dependencies {
    compile 'com.cloudbees.clickstack:clickstack-framework:1.0.12'
    compile 'com.fasterxml.jackson.core:jackson-core:2.1.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.1.3'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.guava:guava:15.0'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-simple:1.7.5'

    testCompile 'junit:junit:4.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.xmlmatchers:xml-matchers:1.0-RC1'

    deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'

    
    clickStackPackage files('/home/dbeer/Software-Development/wildfly/wildfly/build/target/wildfly-8.0.1.Final-SNAPSHOT.zip')
    clickStackControl('com.cloudbees:cloudbees-jmx-invoker:1.0.2:jar-with-dependencies') {
        transitive = false
    }
    clickStackRuntimeJavaAgent 'org.jmxtrans.agent:jmxtrans-agent:1.0.6'
    clickStackRuntimeJavaAgent 'com.cloudbees.clickstack:cloudbees-clickstack-javaagent:1.2.0'
    clickStackRuntime 'com.cloudbees:cloudbees-web-container-extras:1.0.1'
    clickStackRuntimePostgresql 'postgresql:postgresql:9.1-901-1.jdbc4'
    clickStackRuntimeMySql 'mysql:mysql-connector-java:5.1.25'
    // we need couchbase-client as we store in membase
    // https://code.google.com/p/memcached-session-manager/wiki/SetupAndConfiguration
    // clickStackRuntimeMemcache 'de.javakaffee.msm:memcached-session-manager-tc7:1.6.5'
    // clickStackRuntimeMemcache 'de.javakaffee.msm:msm-kryo-serializer:1.6.5'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": rootProject.name,
                "Implementation-Artifact": "$rootProject.group:$rootProject.name:$rootProject.version",
                "Implementation-Version": rootProject.version,
                "Implementation-Date": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Implementation-URL": "https://github.com/CloudBees-community/wildfly-clickstack",
                "Implementation-DocURL": "http://developer.cloudbees.com/bin/view/RUN/Wildfly",
                "Implementation-Vendor": "CloudBees",
                "Implementation-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

idea {
    project {
        languageLevel = '1.7'
    }
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath 'com.cloudbees.clickstack:gradle-clickstack-plugin:1.0.3'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}
repositories {
    mavenCentral()
    maven {
        // as documented on https://code.google.com/p/spymemcached/wiki/Maven
        url "http://files.couchbase.com/maven2/"
        // wildfly nightly builds
        url "http://repository-community.forge.cloudbees.com/release/"
    }
    mavenLocal()
}

release {
    requireBranch = ''
    tagPrefix = 'v'
}
createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            // see http://issues.gradle.org/browse/GRADLE-1826
            String username = project.hasProperty('repoUsername') ? project.getProperty('repoUsername') : "NOT_DEFINED"
            String password = project.hasProperty('repoPassword') ? project.getProperty('repoPassword') : "NOT_DEFINED"
            logger.quiet("Use username: $username to upload archives")

            configuration = configurations.deployerJars
            repository(url: "dav:https://repository-community.forge.cloudbees.com/release") {
                authentication(userName: username, password: password)

            }
            snapshotRepository(url: "dav:https://repository-community.forge.cloudbees.com/snapshot") {
                authentication(userName: username, password: password)
            }
        }
    }
}
